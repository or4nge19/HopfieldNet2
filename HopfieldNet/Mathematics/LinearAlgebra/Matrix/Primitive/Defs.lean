import HopfieldNet.Mathematics.aux
import HopfieldNet.Mathematics.LinearAlgebra.Matrix.Spectrum
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.Normed.Field.Instances
import Mathlib.Analysis.RCLike.Lemmas
import Mathlib.Combinatorics.Quiver.Path
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Metrizable.CompletelyMetrizable
import HopfieldNet.Combinatorics.Quiver.Path
import HopfieldNet.Topology.Compactness.ExtremeValueUSC

/-!
# Perron-Frobenius Theory for Matrices

This file develops the essential Perron-Frobenius theory for non-negative matrices.
It provides the spectral analysis of non-negative irreducible matrices, which underlies
applications like Markov chain convergence and spectral graph theory.

## Main Definitions

* `Matrix.toQuiver`: The directed graph associated with a matrix `A`.
* `Matrix.Irreducible`: A matrix `A` is irreducible if its associated directed graph is
  strongly connected.
* `Matrix.IsPrimitive`: A matrix `A` is primitive if some power of it is strictly positive.

## Main Results

The file builds towards the Perron-Frobenius theorem by formalizing key characterizations:

* `Matrix.irreducible_iff_exists_pow_pos`: A combinatorial characterization of
  irreducibility.
* `Matrix.pow_entry_pos_iff_exists_path`: A fundamental link between matrix powers and
  path existence in the associated graph.
-/

namespace Matrix
open Finset Quiver
variable {n : Type*} [Fintype n]

lemma pow_nonneg [DecidableEq n] {A : Matrix n n ‚Ñù} (hA : ‚àÄ i j, 0 ‚â§ A i j) (k : ‚Ñï) : ‚àÄ i j, 0 ‚â§ (A ^ k) i j := by
  induction k with
  | zero => intro i j; simp [one_apply]; by_cases h : i = j <;> simp [h]
  | succ m ih =>
    intro i j; rw [pow_succ, mul_apply]
    exact Finset.sum_nonneg fun l _ => mul_nonneg (ih i l) (hA l j)

def toQuiver (A : Matrix n n ‚Ñù) : Quiver n :=
  ‚ü®fun i j => 0 < A i j‚ü©

abbrev IsStronglyConnected (G : Quiver n) : Prop := ‚àÄ i j : n, Nonempty (Quiver.Path i j)

def Irreducible (A : Matrix n n ‚Ñù) : Prop :=
  IsStronglyConnected (toQuiver A)

def IsPrimitive [DecidableEq n] (A : Matrix n n ‚Ñù) : Prop :=
  ‚àÉ k, k > 0 ‚àß ‚àÄ i j, 0 < (A ^ k) i j

/-- If `A` is irreducible and `n>1` then every row has a positive entry. -/
lemma irreducible_no_zero_row
    (A : Matrix n n ‚Ñù) --(hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j)
    (h_irr : Irreducible A) (h_dim : 1 < Fintype.card n) (i : n) :
    ‚àÉ j, 0 < A i j := by
  by_contra h_row ; push_neg at h_row   -- `h_row : ‚àÄ j, A i j ‚â§ 0`
  letI G := toQuiver A
  have no_out : ‚àÄ j : n, IsEmpty (i ‚ü∂ j) :=
    fun j ‚Ü¶ ‚ü®fun h ‚Ü¶ (h_row j).not_lt h‚ü©
  obtain ‚ü®j, hij‚ü© := Fintype.exists_ne_of_one_lt_card h_dim i
  obtain ‚ü®p‚ü© : Nonempty (G.Path i j) := h_irr i j
  have : False := by
    have aux : (‚àÄ {v} (q : G.Path i v), v ‚â† i ‚Üí False) := by
      intro v q
      induction q with
      | nil =>
          intro hvi ; exact hvi rfl
      | cons r e ih =>
          cases r with
          | nil =>
              intro _
              exact (no_out _).false e
          | cons r' e' =>
              intro hvi ; simp_all only [isEmpty_Prop, ne_eq, imp_false, not_not, G]
    exact aux p hij
  exact this.elim

section PerronFrobeniusTheorems

variable {A : Matrix n n ‚Ñù}

open Classical in
lemma sum_pos_of_mem  {Œ± : Type*} {s : Finset Œ±} {f : Œ± ‚Üí ‚Ñù}
    (h_nonneg : ‚àÄ a ‚àà s, 0 ‚â§ f a) (a : Œ±) (ha_mem : a ‚àà s) (ha_pos : 0 < f a) :
    0 < ‚àë x ‚àà s, f x := by
  have h_sum_split : ‚àë x ‚àà s, f x = f a + ‚àë x ‚àà s.erase a, f x :=
    Eq.symm (add_sum_erase s f ha_mem)
  have h_erase_nonneg : 0 ‚â§ ‚àë x ‚àà s.erase a, f x :=
    Finset.sum_nonneg (Œª x hx => h_nonneg x (Finset.mem_of_mem_erase hx))
  rw [h_sum_split]
  exact add_pos_of_pos_of_nonneg ha_pos h_erase_nonneg

theorem pow_entry_pos_iff_exists_path [DecidableEq n] (hA : ‚àÄ i j, 0 ‚â§ A i j) (k : ‚Ñï) (i j : n) :
    letI := toQuiver A; 0 < (A ^ k) i j ‚Üî Nonempty {p : Path i j // p.length = k} := by
  induction k generalizing i j with
  | zero =>
    simp only [pow_zero, one_apply, Quiver.Path.length_nil, gt_iff_lt, zero_lt_one, nonempty_subtype]
    constructor
    ¬∑ intro h_pos
      split_ifs at h_pos with h_eq
      ¬∑ subst h_eq; exact ‚ü®letI := toQuiver A; Quiver.Path.nil, rfl‚ü©
      ¬∑ exfalso; linarith [h_pos]
    ¬∑ rintro ‚ü®p, hp‚ü©
      have h_eq : i = j := letI := toQuiver A; Quiver.Path.eq_of_length_zero p hp
      simp [h_eq]
  | succ m ih =>
    rw [pow_succ, mul_apply, nonempty_subtype]
    constructor
    ¬∑ intro h_pos
      obtain ‚ü®l, hl_mem, hl_pos‚ü© : ‚àÉ l ‚àà univ, 0 < (A ^ m) i l * A l j :=
        exists_mem_of_sum_pos h_pos fun x _ => mul_nonneg (pow_nonneg hA m i x) (hA x j)
      rcases (mul_pos_iff_of_nonneg (pow_nonneg hA m i l) (hA l j)).mp hl_pos with ‚ü®h_Am, h_A‚ü©
      obtain ‚ü®‚ü®p, hp_len‚ü©‚ü© := (ih i l).mp h_Am
      exact ‚ü®letI := toQuiver A; p.cons h_A, by simp [Quiver.Path.length_cons, hp_len]‚ü©
    ¬∑ rintro ‚ü®p, hp_len‚ü©
      cases p with
      | nil => simp [Quiver.Path.length] at hp_len
      | @cons b _ q e =>
        simp only [Quiver.Path.length_cons, Nat.succ.injEq] at hp_len
        have h_Am_pos : 0 < (A ^ m) i b := (ih i b).mpr ‚ü®q, hp_len‚ü©
        let h_A_pos := e
        have h_prod : 0 < (A ^ m) i b * A b j := mul_pos h_Am_pos h_A_pos
        exact sum_pos_of_mem
          (fun x _ => mul_nonneg (pow_nonneg hA m i x) (hA x j))
          b
          (Finset.mem_univ b)
          h_prod

theorem irreducible_iff_exists_pow_pos [DecidableEq n] (hA : ‚àÄ i j, 0 ‚â§ A i j) :
    Irreducible A ‚Üî ‚àÄ i j, ‚àÉ k, 0 < (A ^ k) i j := by
  constructor
  ¬∑ intro h_irr i j
    rcases h_irr i j with ‚ü®p‚ü©
    use letI := toQuiver A; p.length
    rw [pow_entry_pos_iff_exists_path hA]
    exact ‚ü®p, rfl‚ü©
  ¬∑ intro h_exists i j
    obtain ‚ü®k, hk_pos‚ü© := h_exists i j
    rw [pow_entry_pos_iff_exists_path hA] at hk_pos
    rcases hk_pos with ‚ü®p, _‚ü©
    exact ‚ü®p‚ü©

theorem IsPrimitive.to_Irreducible [DecidableEq n] (h_prim : IsPrimitive A) (hA : ‚àÄ i j, 0 ‚â§ A i j) :
    Irreducible A := by
  rw [irreducible_iff_exists_pow_pos hA]
  intro i j
  obtain ‚ü®k, _hk_gt_zero, hk_pos‚ü© := h_prim
  exact ‚ü®k, hk_pos i j‚ü©

end PerronFrobeniusTheorems
end Matrix

/-!
### The Perron-Frobenius Theorem for Primitive Matrices

This section formalizes Theorem 1.1 from Seneta's "Non-negative Matrices and Markov Chains".
The proof follows Seneta's logic precisely:
1. Define the Perron root `r` as the supremum of the Collatz-Wielandt function `r(x)`.
2. Use the fact that `r(x)` is upper-semicontinuous on a compact set (the standard simplex)
   to guarantee the supremum is attained by a vector `v`.
3. Prove that `v` is an eigenvector by a contradiction argument using the primitivity of `A`.
4. Prove that `v` is strictly positive, again using primitivity.
-/
namespace Matrix
section PerronFrobeniusMain
variable {n : Type*} [Fintype n] [Nonempty n]
variable {A : Matrix n n ‚Ñù}

open LinearMap Set Filter Topology Finset
open scoped Convex Pointwise

/-- The Collatz-Wielandt function, `r(x)` in Seneta's notation.
For a non-zero, non-negative vector `x`, this is `min_{i | x·µ¢ > 0} (Ax)·µ¢ / x·µ¢`.
Seneta uses row vectors `x'T`; we use column vectors `Ax`. The logic is identical. -/
noncomputable def collatzWielandtFn (A : Matrix n n ‚Ñù) (x : n ‚Üí ‚Ñù) : ‚Ñù :=
  let supp := {i | 0 < x i}.toFinset
  if h : supp.Nonempty then
    supp.inf' h fun i => (A *·µ• x) i / x i
  else 0

/-
/-- The Collatz-Wielandt function r_x for a positive vector `x`.
    See p. 30 in Berman & Plemmons.
    We define it for strictly positive vectors to avoid division by zero. -/
noncomputable def r_x (A : Matrix n n ‚Ñù) (x : n ‚Üí ‚Ñù) : ‚Ñù :=
  ‚®Ö i, (A.mulVec x i) / (x i)-/

instance : Nonempty n := inferInstance

/-- For a finite type, the infimum over the type is attained at some element. -/
lemma exists_eq_iInf {Œ± : Type*} [Fintype Œ±] [Nonempty Œ±] (f : Œ± ‚Üí ‚Ñù) : ‚àÉ i, f i = ‚®Ö j, f j :=
  exists_eq_ciInf_of_finite

/-- The matrix-vector multiplication map as a continuous linear map. -/
noncomputable abbrev mulVec_continuousLinearMap (A : Matrix n n ‚Ñù) : (n ‚Üí ‚Ñù) ‚ÜíL[‚Ñù] (n ‚Üí ‚Ñù) :=
  (Matrix.mulVecLin A).toContinuousLinearMap

/-- Functions computing pointwise infima are equal when using `iInf` vs `Finset.inf'`. -/
lemma Finset.iInf_apply_eq_finset_inf'_apply_fun {Œ± Œ≤ Œ≥ : Type*}
    [Fintype Œ±] [Nonempty Œ±] [ConditionallyCompleteLinearOrder Œ≥]
    (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) :
    (fun x ‚Ü¶ ‚®Ö i, f i x) = (fun x ‚Ü¶ (Finset.univ : Finset Œ±).inf' Finset.univ_nonempty (fun i ‚Ü¶ f i x)) := by
  ext x
  have h1 : ‚®Ö i, f i x = ‚®Ö i ‚àà Set.univ, f i x := by
    simp only [mem_univ, ciInf_unique]
  have h2 : ‚®Ö i ‚àà Set.univ, f i x = ‚®Ö i ‚àà (Finset.univ : Finset Œ±), f i x := by
    congr
    ext i
    simp only [mem_univ, ciInf_unique, Finset.mem_univ]
  have h3 : ‚®Ö i ‚àà (Finset.univ : Finset Œ±), f i x =
           (Finset.univ : Finset Œ±).inf' Finset.univ_nonempty (fun i ‚Ü¶ f i x) := by
    rw [Finset.inf'_eq_csInf_image]
    simp only [mem_univ, ciInf_unique, Finset.mem_univ, Finset.coe_univ, image_univ]
    rfl
  rw [h1, h2, h3]

/-- For a finite index type, the point-wise (finite) infimum of a family of
    continuous functions is continuous. -/
lemma continuousOn_iInf {Œ± Œ≤ : Type*}
    [Fintype Œ±] [Nonempty Œ±]
    [TopologicalSpace Œ≤]
    {s : Set Œ≤} {f : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù}
    (hf : ‚àÄ i, ContinuousOn (f i) s) :
    ContinuousOn (fun x ‚Ü¶ ‚®Ö i, f i x) s := by
  classical
  let g : Œ≤ ‚Üí ‚Ñù :=
    fun x ‚Ü¶ (Finset.univ : Finset Œ±).inf' Finset.univ_nonempty (fun i ‚Ü¶ f i x)
  have hg : ContinuousOn g s := by
    exact ContinuousOn.finset_inf'_apply Finset.univ_nonempty fun i a ‚Ü¶ hf i
  have h_eq : (fun x ‚Ü¶ ‚®Ö i, f i x) = g := by
    dsimp [g]
    exact Finset.iInf_apply_eq_finset_inf'_apply_fun f
  rwa [h_eq]

/-
-- The Collatz-Wielandt function r_x is continuous on the set of strictly positive vectors.
lemma r_x_continuousOn_pos (A : Matrix n n ‚Ñù) :
    ContinuousOn (r_x A) {x : n ‚Üí ‚Ñù | ‚àÄ i, 0 < x i} := by
  unfold r_x
  apply continuousOn_iInf
  intro i
  apply ContinuousOn.div
  ¬∑ exact ((continuous_apply i).comp (mulVec_continuousLinearMap A).continuous).continuousOn
  ¬∑ exact (continuous_apply i).continuousOn
  ¬∑ exact fun x a ‚Ü¶ Ne.symm (ne_of_lt (a i))-/

/-- The Collatz-Wielandt function is upper-semicontinuous.
Seneta relies on this fact (p.15, Appendix C) to use the Extreme Value Theorem.
`r(x)` is a minimum of functions `f·µ¢(x) = (Ax)·µ¢ / x·µ¢`. Each `f·µ¢` is continuous where `x·µ¢ > 0`.
The minimum of continuous functions is upper-semicontinuous. -/
theorem upperSemicontinuousOn_collatzWielandtFn
    (A : Matrix n n ‚Ñù) : UpperSemicontinuousOn (collatzWielandtFn A) (stdSimplex ‚Ñù n) := by
    have support_nonempty : ‚àÄ x ‚àà stdSimplex ‚Ñù n, ({i | 0 < x i}.toFinset).Nonempty := by
      intro x hx
      obtain ‚ü®i, hi_pos‚ü© := exists_pos_of_sum_one_of_nonneg hx.2 hx.1
      have hi_mem : i ‚àà ({i | 0 < x i}.toFinset) := by simp [hi_pos]
      exact ‚ü®i, hi_mem‚ü©
    intro x‚ÇÄ hx‚ÇÄ c hc
    have supp_x‚ÇÄ : {i | 0 < x‚ÇÄ i}.toFinset.Nonempty := support_nonempty x‚ÇÄ hx‚ÇÄ
    have fn_eq : collatzWielandtFn A x‚ÇÄ = {i | 0 < x‚ÇÄ i}.toFinset.inf' supp_x‚ÇÄ (fun i => (A *·µ• x‚ÇÄ) i / x‚ÇÄ i) := by
      unfold collatzWielandtFn
      rw [dif_pos supp_x‚ÇÄ]
    let U := {y : n ‚Üí ‚Ñù | ‚àÄ i ‚àà {i | 0 < x‚ÇÄ i}.toFinset, 0 < y i}
    have x‚ÇÄ_in_U : x‚ÇÄ ‚àà U := by
      intro i hi
      simp only [Set.mem_toFinset] at hi
      exact hi
    have U_open : IsOpen U := by
      have h_eq : U = ‚ãÇ i ‚àà {i | 0 < x‚ÇÄ i}.toFinset, {y | 0 < y i} := by
        ext y
        simp only [Set.mem_iInter, Set.mem_setOf_eq]
        rfl
      rw [h_eq]
      apply isOpen_biInter_finset
      intro i _
      exact isOpen_lt continuous_const (continuous_apply i)
    let f := fun y => {i | 0 < x‚ÇÄ i}.toFinset.inf' supp_x‚ÇÄ fun i => (A *·µ• y) i / y i
    have f_cont : ContinuousOn f U := by
      apply continuousOn_finset_inf' supp_x‚ÇÄ
      intro i hi
      apply ContinuousOn.div
      ¬∑ exact continuous_apply i |>.comp_continuousOn ((mulVec_continuousLinearMap A).continuous.continuousOn)
      ¬∑ exact (continuous_apply i).continuousOn
      ¬∑ intro y hy
        simp only [Set.mem_setOf_eq] at hy
        exact (hy i hi).ne'
    have f_ge : ‚àÄ y ‚àà U ‚à© stdSimplex ‚Ñù n, collatzWielandtFn A y ‚â§ f y := by
      intro y hy
      have y_supp : {i | 0 < y i}.toFinset.Nonempty := support_nonempty y hy.2
      have fn_y : collatzWielandtFn A y = {i | 0 < y i}.toFinset.inf' y_supp fun i => (A *·µ• y) i / y i := by
        unfold collatzWielandtFn
        rw [dif_pos y_supp]
      have supp_subset : {i | 0 < x‚ÇÄ i}.toFinset ‚äÜ {i | 0 < y i}.toFinset := by
        intro i hi
        have hi' : 0 < x‚ÇÄ i := by
          simp only [Set.mem_toFinset] at hi
          exact hi
        have : 0 < y i := hy.1 i hi
        simp only [Set.mem_toFinset]
        exact this
      rw [fn_y]
      apply finset_inf'_mono_subset supp_subset
    rw [fn_eq] at hc
    have : f x‚ÇÄ < c := hc
    have cont_at : ContinuousAt f x‚ÇÄ := f_cont.continuousAt (IsOpen.mem_nhds U_open x‚ÇÄ_in_U)
    have lt_eventually : ‚àÄ·∂† y in ùìù x‚ÇÄ, f y < c :=
      Filter.Tendsto.eventually_lt_const hc cont_at
    rcases eventually_to_open lt_eventually with ‚ü®V, V_open, x‚ÇÄ_in_V, hV‚ü©
    let W := V ‚à© U ‚à© stdSimplex ‚Ñù n
    have VU_open : IsOpen (V ‚à© U) := IsOpen.inter V_open U_open
    have VU_mem : x‚ÇÄ ‚àà V ‚à© U := ‚ü®x‚ÇÄ_in_V, x‚ÇÄ_in_U‚ü©
    have VU_nhds : V ‚à© U ‚àà ùìù x‚ÇÄ := VU_open.mem_nhds VU_mem
    have W_nhdsWithin : W ‚àà ùìù[stdSimplex ‚Ñù n] x‚ÇÄ := by
      rw [mem_nhdsWithin_iff_exists_mem_nhds_inter]
      exact ‚ü®V ‚à© U, VU_nhds, by simp [W]‚ü©
    have h_final : ‚àÄ y ‚àà W, collatzWielandtFn A y < c := by
      intro y hy
      apply lt_of_le_of_lt
      ¬∑ apply f_ge y
        exact ‚ü®And.right (And.left hy), hy.2‚ü©
      ¬∑ exact hV y (And.left (And.left hy))
    exact Filter.mem_of_superset W_nhdsWithin (fun y hy => h_final y hy)

end PerronFrobeniusMain

end Matrix

open Set Finset MetricSpace Topology Convex Quiver.Path

namespace Matrix
variable {n : Type*} --[Fintype n]

open Topology Metric Set Finset
section PerronFrobenius
open Finset Set IsCompact Topology Matrix
-- The set of vectors we are optimizing over.
def P_set := {x : n ‚Üí ‚Ñù | (‚àÄ i, 0 ‚â§ x i) ‚àß x ‚â† 0}

-- The Collatz-Wielandt value is the supremum of `r_x` over P.
noncomputable def r (A : Matrix n n ‚Ñù) [Fintype n] := ‚®Ü x ‚àà P_set, collatzWielandtFn A x

-- We define `y = (I + A)^(n-1) * x`. For any non-zero non-negative `x`, `y` is strictly positive.
-- This is from Theorem 1.3(d) on page 48.
-- Helper lemma: if `A` has strictly positive entries and `x` is a
-- non-negative, non-zero vector, then every entry of `A ‚Ä¢ x` is positive.
lemma positive_mul_vec_pos [Fintype n]
    {A : Matrix n n ‚Ñù} (hA_pos : ‚àÄ i j, 0 < A i j)
    {x : n ‚Üí ‚Ñù} (hx_nonneg : ‚àÄ i, 0 ‚â§ x i) (hx_ne_zero : x ‚â† 0) :
    ‚àÄ i, 0 < (A.mulVec x) i := by
  intro i
  --  `A.mulVec x i = ‚àë j, A i j * x j`
  simp only [Matrix.mulVec, dotProduct]
  apply Finset.sum_pos'
  ¬∑ intro j _
    exact mul_nonneg (le_of_lt (hA_pos i j)) (hx_nonneg j)
  ¬∑ have : ‚àÉ k, 0 < x k := by
      by_contra h_all_nonpos
      push_neg at h_all_nonpos
      have h_zero : x = 0 := funext (fun j => le_antisymm (h_all_nonpos j) (hx_nonneg j))
      exact hx_ne_zero h_zero
    rcases this with ‚ü®k, hk_pos‚ü©
    refine ‚ü®k, ?_, ?_‚ü©
    ¬∑ simp only [Finset.mem_univ]  --  `k ‚àà Finset.univ`
    ¬∑ exact mul_pos (hA_pos i k) hk_pos

variable {A : Matrix n n ‚Ñù} --[DecidableEq n] [Nonempty n]

theorem positive_mul_vec_of_nonneg_vec [Fintype n] (hA_pos : ‚àÄ i j, 0 < A i j)
    {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i) (hv_ne_zero : v ‚â† 0) :
    ‚àÄ i, 0 < (A *·µ• v) i := by
  intro i
  simp only [mulVec, dotProduct]
  apply Finset.sum_pos'
  ¬∑ intro j _
    exact mul_nonneg (le_of_lt (hA_pos i j)) (hv_nonneg j)
  ¬∑ have : ‚àÉ k, 0 < v k := by
      by_contra h_all_nonpos
      push_neg at h_all_nonpos
      have h_zero : v = 0 := funext (fun j => le_antisymm (h_all_nonpos j) (hv_nonneg j))
      exact hv_ne_zero h_zero
    rcases this with ‚ü®k, hk_pos‚ü©
    refine ‚ü®k, Finset.mem_univ k, ?_‚ü©
    exact mul_pos (hA_pos i k) hk_pos

/-- If an entry `A i j` is positive, there is a path from `i` to `j` in the quiver. -/
lemma path_exists_of_pos_entry {A : Matrix n n ‚Ñù} {i j : n} (h_pos : 0 < A i j) :
    letI := toQuiver A; Nonempty (Quiver.Path i j) := by
  letI G := toQuiver A
  let e : G.Hom i j := h_pos
  exact ‚ü®e.toPath‚ü©

/-- A matrix with all positive entries is irreducible. -/
lemma irreducible_of_all_entries_positive {A : Matrix n n ‚Ñù} (hA : ‚àÄ i j, 0 < A i j) :
    Irreducible A := by
  unfold Irreducible IsStronglyConnected
  intro i j
  exact path_exists_of_pos_entry (hA i j)

theorem exists_connecting_edge_of_irreducible {A : Matrix n n ‚Ñù} (hA_irred : A.Irreducible)
    {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (S T : Set n) (hS_nonempty : S.Nonempty) (hT_nonempty : T.Nonempty)
    (h_partition : ‚àÄ i, i ‚àà S ‚Üî v i > 0)
    (h_complement : ‚àÄ i, i ‚àà T ‚Üî v i = 0) :
    ‚àÉ (i j : n), i ‚àà T ‚àß j ‚àà S ‚àß 0 < A i j := by
  obtain ‚ü®i‚ÇÄ, hi‚ÇÄ_in_T‚ü© := hT_nonempty
  obtain ‚ü®j‚ÇÄ, hj‚ÇÄ_in_S‚ü© := hS_nonempty
  unfold Irreducible IsStronglyConnected toQuiver at hA_irred
  obtain ‚ü®p‚ü© := hA_irred j‚ÇÄ i‚ÇÄ
  obtain ‚ü®y, z, e, _, _, hy_not_T, hz_in_T, _‚ü© :=
    @Quiver.Path.exists_boundary_edge n (toQuiver A) _ _ p T
    (fun h_j0_in_T => by
      have hj‚ÇÄ_pos : v j‚ÇÄ > 0 := (h_partition j‚ÇÄ).mp hj‚ÇÄ_in_S
      have hj‚ÇÄ_zero : v j‚ÇÄ = 0 := (h_complement j‚ÇÄ).mp h_j0_in_T
      exact ne_of_gt hj‚ÇÄ_pos hj‚ÇÄ_zero)
    hi‚ÇÄ_in_T
  have hy_in_S : y ‚àà S := by
    by_contra hy_not_S
    have hy_in_T : y ‚àà T := by
      cases' (lt_or_eq_of_le (hv_nonneg y)) with h_pos h_zero
      ¬∑ rename_i right
        subst right
        simp_all only [gt_iff_lt, not_true_eq_false]
      ¬∑ rename_i right
        subst right
        simp_all only [gt_iff_lt, not_true_eq_false]
    exact hy_not_T hy_in_T
  obtain ‚ü®p'‚ü© := hA_irred i‚ÇÄ j‚ÇÄ
  obtain ‚ü®y, z, e, _, _, hy_not_S, hz_in_S, _‚ü© :=
    @Quiver.Path.exists_boundary_edge n (toQuiver A) _ _ p' S
    (fun h_i0_in_S => by
      have hi‚ÇÄ_zero : v i‚ÇÄ = 0 := (h_complement i‚ÇÄ).mp hi‚ÇÄ_in_T
      have hi‚ÇÄ_pos : v i‚ÇÄ > 0 := (h_partition i‚ÇÄ).mp h_i0_in_S
      exact ne_of_gt hi‚ÇÄ_pos hi‚ÇÄ_zero)
    hj‚ÇÄ_in_S
  have hy_in_T : y ‚àà T := by
    by_contra hy_not_T
    have hy_in_S : y ‚àà S := by
      cases' (lt_or_eq_of_le (hv_nonneg y)) with h_pos h_zero
      ¬∑ exact (h_partition y).mpr h_pos
      ¬∑ have hy_in_T' : y ‚àà T := by aesop
        exact (hy_not_T hy_in_T').elim
    exact hy_not_S hy_in_S
  exact ‚ü®y, z, hy_in_T, hz_in_S, e‚ü©

lemma exists_boundary_crossing_in_support [DecidableEq n] [Fintype n]
    (hA_irred : Irreducible A) (_ : ‚àÄ i j, 0 ‚â§ A i j)
    {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i) (_ : v ‚â† 0)
    (S T : Set n) (hS_nonempty : S.Nonempty) (hT_nonempty : T.Nonempty)
    (h_partition : ‚àÄ i, i ‚àà S ‚Üî v i > 0)
    (h_complement : ‚àÄ i, i ‚àà T ‚Üî v i = 0) :
    ‚àÉ (i j : n), i ‚àà T ‚àß j ‚àà S ‚àß 0 < A i j := by
  obtain ‚ü®i‚ÇÄ, hi‚ÇÄ_in_T‚ü© := hT_nonempty
  obtain ‚ü®j‚ÇÄ, hj‚ÇÄ_in_S‚ü© := hS_nonempty
  unfold Irreducible IsStronglyConnected toQuiver at hA_irred
  obtain ‚ü®p‚ü© := hA_irred i‚ÇÄ j‚ÇÄ
  obtain ‚ü®y, z, e, _, _, hy_not_S, hz_in_S, _‚ü© :=
    @Quiver.Path.exists_boundary_edge n (toQuiver A) _ _ p S
    (fun h_i0_in_S => by
      have hi‚ÇÄ_zero : v i‚ÇÄ = 0 := (h_complement i‚ÇÄ).mp hi‚ÇÄ_in_T
      have hi‚ÇÄ_pos : v i‚ÇÄ > 0 := (h_partition i‚ÇÄ).mp h_i0_in_S
      exact ne_of_gt hi‚ÇÄ_pos hi‚ÇÄ_zero)
    hj‚ÇÄ_in_S
  have hy_in_T : y ‚àà T := by
    by_contra hy_not_T
    have hy_in_S : y ‚àà S := by
      cases' (lt_or_eq_of_le (hv_nonneg y)) with h_pos h_zero
      ¬∑ exact (h_partition y).mpr h_pos
      ¬∑ have hy_in_T' : y ‚àà T := by aesop
        exact (hy_not_T hy_in_T').elim
    exact hy_not_S hy_in_S
  exact ‚ü®y, z, hy_in_T, hz_in_S, e‚ü©

theorem irreducible_mulVec_ne_zero [DecidableEq n] [Fintype n]
    (hA_irred : Irreducible A) (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) (hA_ne_zero : A ‚â† 0)
    {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i) (hv_ne_zero : v ‚â† 0) :
    A *·µ• v ‚â† 0 := by
  by_contra h_Av_zero
  let S : Set n := {i | v i > 0}
  let T : Set n := {i | v i = 0}
  have hS_nonempty : S.Nonempty := by
    by_contra hS_empty
    rw [Set.not_nonempty_iff_eq_empty] at hS_empty
    apply hv_ne_zero
    ext k
    have : v k ‚â§ 0 := by
      by_contra hv_pos
      have : k ‚àà S := not_le.mp hv_pos
      rw [hS_empty] at this
      exact Set.not_mem_empty k this
    exact le_antisymm this (hv_nonneg k)
  by_cases hT_is_empty : T = ‚àÖ
  ¬∑ -- All entries positive case
    have v_all_pos : ‚àÄ i, v i > 0 := by
      intro i
      have hi_not_in_T : i ‚àâ T := by simp [hT_is_empty]
      have hi_ne_zero : v i ‚â† 0 := by simpa [T] using hi_not_in_T
      exact lt_of_le_of_ne (hv_nonneg i) (id (Ne.symm hi_ne_zero))
    have A_is_zero : A = 0 := by
      ext k j
      have : (A *·µ• v) k = 0 := congrFun h_Av_zero k
      rw [mulVec, dotProduct] at this
      have terms_nonneg : ‚àÄ idx, 0 ‚â§ A k idx * v idx :=
        fun idx => mul_nonneg (hA_nonneg k idx) (le_of_lt (v_all_pos idx))
      have term_kj_is_zero := (Finset.sum_eq_zero_iff_of_nonneg (fun i _ => terms_nonneg i)).mp this j (Finset.mem_univ _)
      exact (mul_eq_zero.mp term_kj_is_zero).resolve_right (v_all_pos j).ne'
    exact hA_ne_zero A_is_zero
  ¬∑ -- Mixed case: we use boundary crossing
    have hT_nonempty : T.Nonempty := Set.nonempty_iff_ne_empty.mpr hT_is_empty
    obtain ‚ü®i, j, hi_T, hj_S, hA_ij_pos‚ü© := exists_boundary_crossing_in_support
      hA_irred hA_nonneg hv_nonneg hv_ne_zero S T hS_nonempty hT_nonempty
      (fun i => by simp [S]) (fun i => by simp [T])
    have hA_ij_zero : A i j = 0 := by
      have : (A *·µ• v) i = 0 := congrFun h_Av_zero i
      rw [mulVec, dotProduct] at this
      have terms_nonneg : ‚àÄ k ‚àà Finset.univ, 0 ‚â§ A i k * v k :=
        fun k _ => mul_nonneg (hA_nonneg i k) (hv_nonneg k)
      have term_j_is_zero := (Finset.sum_eq_zero_iff_of_nonneg terms_nonneg).mp this j (Finset.mem_univ _)
      have hv_j_pos : v j > 0 := by simp [S] at hj_S; exact hj_S
      exact (mul_eq_zero.mp term_j_is_zero).resolve_right (ne_of_gt hv_j_pos)
    exact (ne_of_gt hA_ij_pos) hA_ij_zero

-- Additional helper for the main theorem
theorem exists_collatzWielandt_maximizer [Fintype n] [Nonempty n] :
    ‚àÉ v ‚àà stdSimplex ‚Ñù n, IsMaxOn (collatzWielandtFn A) (stdSimplex ‚Ñù n) v := by
  have h_compact : IsCompact (stdSimplex ‚Ñù n) := isCompact_stdSimplex
  have h_nonempty : (stdSimplex ‚Ñù n).Nonempty := stdSimplex_nonempty
  have h_usc : UpperSemicontinuousOn (collatzWielandtFn A) (stdSimplex ‚Ñù n) :=
    upperSemicontinuousOn_collatzWielandtFn A
  exact IsCompact.exists_isMaxOn_of_upperSemicontinuousOn h_compact h_nonempty h_usc

/-- The standard simplex in ‚Ñù‚Åø is compact (Heine-Borel: closed and bounded in ‚Ñù‚Åø).
    [Giaquinta-Modica, Theorem 6.3, cite: 230] -/
lemma IsCompact_stdSimplex [Fintype n] : IsCompact (stdSimplex ‚Ñù n) := by
  -- stdSimplex is a closed and bounded subset of ‚Ñù‚Åø
  exact _root_.isCompact_stdSimplex n

/-- The Collatz-Wielandt function is upper semicontinuous on the standard simplex.
    [Giaquinta-Modica, Definition 6.21, Exercise 6.28, cite: 235, 236] -/
lemma UpperSemicontinuousOn_collatzWielandtFn [Fintype n]  [Nonempty n]
    (A : Matrix n n ‚Ñù) : UpperSemicontinuousOn (Matrix.collatzWielandtFn A) (stdSimplex ‚Ñù n) := by
  exact Matrix.upperSemicontinuousOn_collatzWielandtFn A

/-- The Collatz-Wielandt function attains its maximum on the standard simplex.
    [Giaquinta-Modica, Theorem 6.24 (dual), cite: 235] -/
theorem exists_maximum_collatzWielandt [Fintype n] [Nonempty n] (A : Matrix n n ‚Ñù) :
    ‚àÉ v ‚àà stdSimplex ‚Ñù n, ‚àÄ x ‚àà stdSimplex ‚Ñù n,
      Matrix.collatzWielandtFn A x ‚â§ Matrix.collatzWielandtFn A v := by
  exact exists_collatzWielandt_maximizer

theorem collatzWielandtFn_eq_iInf_of_nonempty
  {n : Type*} [Fintype n] [Nonempty n] (A : Matrix n n ‚Ñù)
  (v : n ‚Üí ‚Ñù) (h : {i | 0 < v i}.toFinset.Nonempty) :
  collatzWielandtFn A v =
    ‚®Ö i : {i | 0 < v i}, (A *·µ• v) i / v i := by
  dsimp [collatzWielandtFn]
  rw [dif_pos h]
  rw [Finset.inf'_eq_ciInf h]
  refine Function.Surjective.iInf_congr ?_ (fun b ‚Ü¶ ?_) ?_
  intro i
  ¬∑ simp_all only [toFinset_setOf]
    obtain ‚ü®val, property‚ü© := i
    simp_all only [toFinset_setOf, mem_filter, Finset.mem_univ, true_and]
    apply Subtype.mk
    ¬∑ exact property
  ¬∑ simp_all
    obtain ‚ü®val, property‚ü© := b
    simp_all only [Subtype.mk.injEq, exists_prop, exists_eq_right]
  simp [Set.mem_toFinset]

theorem mul_vec_mul_vec
  {n : Type*} [Fintype n] [Nonempty n] (A B : Matrix n n ‚Ñù) (v : n ‚Üí ‚Ñù) :
  (A * B) *·µ• v = A *·µ• (B *·µ• v) := by
  ext i
  simp only [mulVec, dotProduct, mul_apply]
  simp_rw [Finset.mul_sum]
  rw [Finset.sum_comm]
  simp_rw [Finset.sum_mul]
  rw [Finset.sum_comm]
  rw [Finset.sum_comm]
  simp [mul_assoc]

variable --{n : Type*} [Fintype n] [DecidableEq n]
          {A : Matrix n n ‚Ñù} {r : ‚Ñù}

/-- A non-negative, non-zero vector must have a positive component. -/
lemma exists_pos_of_ne_zero {v : n ‚Üí ‚Ñù} (h_nonneg : ‚àÄ i, 0 ‚â§ v i) (h_ne_zero : v ‚â† 0) :
    ‚àÉ i, 0 < v i := by
  by_contra h_all_nonpos
  apply h_ne_zero
  ext i
  exact le_antisymm (by simp_all only [ne_eq, not_exists, not_lt, Pi.zero_apply]) (h_nonneg i)

/-- A set is nonempty if and only if its finite conversion is nonempty. -/
lemma Set.toFinset_nonempty_iff {Œ± : Type*} [Fintype Œ±] [DecidableEq Œ±] (s : Set Œ±) [Finite s] [Fintype s] :
    s.toFinset.Nonempty ‚Üî s.Nonempty := by
  constructor
  ¬∑ intro h
    obtain ‚ü®x, hx‚ü© := h
    exact ‚ü®x, Set.mem_toFinset.mp hx‚ü©
  ¬∑ intro h
    obtain ‚ü®x, hx‚ü© := h
    exact ‚ü®x, Set.mem_toFinset.mpr hx‚ü©

/-- Division inequality: a / b ‚â§ c ‚Üî a ‚â§ c * b when b > 0. -/
lemma div_le_iff {a b c : ‚Ñù} (hb : 0 < b) : a / b ‚â§ c ‚Üî a ‚â§ c * b := by
  rw [@le_iff_le_iff_lt_iff_lt]
  exact lt_div_iff‚ÇÄ hb

/-- For real numbers, if `0 < b`, then `a ‚â§ c * b ‚Üî a / b ‚â§ c`. -/
lemma le_div_iff {a b c : ‚Ñù} (hb : 0 < b) : a ‚â§ c * b ‚Üî a / b ‚â§ c := by
  rw [‚Üêdiv_le_iff hb]

/-- If r ‚â§ 0 and r is the infimum of non-negative ratios, then r = 0. -/
lemma collatzWielandt_val_eq_zero_of_nonpos [Fintype n] [Nonempty n] [DecidableEq n]
    (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (S : Set n) (hS_def : S = {i | 0 < v i}) (hS_nonempty : S.Nonempty)
    (r : ‚Ñù) (hr_def : r = collatzWielandtFn A v) (hr_nonpos : r ‚â§ 0) :
    r = 0 := by
  have r_ge_zero : 0 ‚â§ r := by
    rw [hr_def, collatzWielandtFn]
    have hS_finset_nonempty : { j | 0 < v j }.toFinset.Nonempty := by
      rwa [Set.toFinset_nonempty_iff, ‚Üê hS_def]
    rw [dif_pos hS_finset_nonempty]
    apply Finset.le_inf'
    intro j hj
    rw [Set.mem_toFinset] at hj
    exact div_nonneg (Finset.sum_nonneg fun k _ => mul_nonneg (hA_nonneg j k) (hv_nonneg k)) hj.le
  exact le_antisymm hr_nonpos r_ge_zero

/-- The ratio (A *·µ• v) i / v i is nonnegative when A has nonnegative entries and v is nonnegative -/
lemma ratio_nonneg [Fintype n] (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (i : n) (hv_pos : 0 < v i) : 0 ‚â§ (A *·µ• v) i / v i :=
  div_nonneg (Finset.sum_nonneg fun j _ => mul_nonneg (hA_nonneg i j) (hv_nonneg j)) hv_pos.le

/-- Each ratio is at least the Collatz-Wielandt value -/
lemma collatzWielandt_le_ratio [Fintype n] [Nonempty n] [DecidableEq n]
    (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (S : Set n) (hS_def : S = {i | 0 < v i}) (hS_nonempty : S.Nonempty)
    (i : n) (hi_S : i ‚àà S) : collatzWielandtFn A v ‚â§ (A *·µ• v) i / v i := by
  rw [collatzWielandtFn]
  have hS_finset_nonempty : { j | 0 < v j }.toFinset.Nonempty := by
    rwa [Set.toFinset_nonempty_iff, ‚Üê hS_def]
  rw [dif_pos hS_finset_nonempty]
  apply Finset.inf'_le
  rw [Set.mem_toFinset, ‚Üê hS_def]
  exact hi_S

lemma Finset.inf'_pos {Œ± : Type*} {s : Finset Œ±} (hs : s.Nonempty)
    {f : Œ± ‚Üí ‚Ñù} (h_pos : ‚àÄ a ‚àà s, 0 < f a) :
    0 < s.inf' hs f := by
  obtain ‚ü®b, hb_mem, h_fb_is_inf‚ü© := s.exists_mem_eq_inf' hs f
  have h_fb_pos : 0 < f b := h_pos b hb_mem
  rw [h_fb_is_inf]
  exact h_fb_pos

lemma lt_not_le {Œ± : Type*} [PartialOrder Œ±] (x y : Œ±) : x < y ‚Üí ¬¨ (x ‚â• y) := by
  intro h_lt h_ge
  exact not_le_of_lt h_lt h_ge

/-- For any non-negative, non-zero vector `v`, the Collatz-Wielandt value `r` satisfies
    `r ‚Ä¢ v ‚â§ A *·µ• v`. This is the fundamental inequality derived from the definition of
    the Collatz-Wielandt function. -/
lemma collatzWielandt_le_mulVec [Fintype n] [Nonempty n] [DecidableEq n]
    (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (hv_ne_zero : v ‚â† 0) :
    (collatzWielandtFn A v) ‚Ä¢ v ‚â§ A *·µ• v := by
  let r := collatzWielandtFn A v
  -- The support of `v` is non-empty because `v` is a non-zero vector.
  have hS_nonempty : ({i | 0 < v i}).Nonempty :=
    exists_pos_of_ne_zero hv_nonneg hv_ne_zero
  intro i
  by_cases hi_supp : v i > 0
  ¬∑ -- If `v i > 0`, then `i` is in the support.
    have h_le_div : r ‚â§ (A *·µ• v) i / v i :=
      collatzWielandt_le_ratio hA_nonneg hv_nonneg {i | 0 < v i} rfl hS_nonempty i hi_supp
    simp only [Pi.smul_apply, smul_eq_mul]
    exact (le_div_iff‚ÇÄ hi_supp).mp h_le_div
  ¬∑ -- If `v i = 0`, we need to show `(r ‚Ä¢ v) i ‚â§ (A * v) i`.
    have h_vi_zero : v i = 0 := by linarith [hv_nonneg i, not_lt.mp hi_supp]
    simp only [Pi.smul_apply, smul_eq_mul, h_vi_zero, mul_zero]
    exact mulVec_nonneg hA_nonneg hv_nonneg i

/-- If the ratio (A *·µ• v) i / v i = 0 and v i > 0, then (A *·µ• v) i = 0. -/
lemma mulVec_eq_zero_of_ratio_zero [Fintype n] {v : n ‚Üí ‚Ñù} (i : n) (hv_pos : 0 < v i)
    (h_ratio_zero : (A *·µ• v) i / v i = 0) :
    (A *·µ• v) i = 0 := by
  rw [div_eq_zero_iff] at h_ratio_zero
  exact h_ratio_zero.resolve_right (ne_of_gt hv_pos)

/-- A zero matrix is not irreducible if the dimension is greater than 1. -/
lemma not_irreducible_of_zero_matrix {n : Type*} [Fintype n] [Nonempty n]
    (h_card_gt_one : 1 < Fintype.card n) : ¬¨ Irreducible (0 : Matrix n n ‚Ñù) := by
  intro h_irred_contra
  obtain ‚ü®i, j, hij‚ü© := Fintype.exists_pair_of_one_lt_card h_card_gt_one
  specialize h_irred_contra i j
  letI := toQuiver (0 : Matrix n n ‚Ñù)
  have h_no_path : ¬¨ Nonempty (Quiver.Path i j) := by
      intro h
      obtain ‚ü®p‚ü© := h
      induction p with
      | nil => exact hij rfl
      | cons p' e ih =>
        exact False.elim (lt_irrefl 0 e)
  exact h_no_path h_irred_contra

/-- If `A *·µ• v` is zero on the support `S` of `v`, then for any `i ‚àà S`, `A i k` must be zero
for all `k` where `v` is positive (i.e., `k ‚àà S`). -/
lemma zero_block_of_mulVec_eq_zero [Fintype n] (hA_nonneg : ‚àÄ i j, 0 ‚â§ A i j) {v : n ‚Üí ‚Ñù} (hv_nonneg : ‚àÄ i, 0 ‚â§ v i)
    (S : Set n) (hS_def : S = {i | 0 < v i})
    (h_Av_zero : ‚àÄ i ‚àà S, (A *·µ• v) i = 0) :
    ‚àÄ i ‚àà S, ‚àÄ k ‚àà S, A i k = 0 := by
  intro i hi_S k hk_S
  have h_sum_Aiv_eq_zero : (A *·µ• v) i = 0 := h_Av_zero i hi_S
  rw [mulVec, dotProduct] at h_sum_Aiv_eq_zero
  have h_sum_terms_nonneg : ‚àÄ l, 0 ‚â§ A i l * v l :=
    fun l ‚Ü¶ mul_nonneg (hA_nonneg i l) (hv_nonneg l)
  have h_Aik_vk_zero : A i k * v k = 0 :=
    (sum_eq_zero_iff_of_nonneg (fun l _ ‚Ü¶ h_sum_terms_nonneg l)).mp h_sum_Aiv_eq_zero k (mem_univ k)
  rw [hS_def] at hk_S
  exact (mul_eq_zero.mp h_Aik_vk_zero).resolve_right (ne_of_gt hk_S)

end PerronFrobenius
end Matrix
